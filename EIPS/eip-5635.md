---
eip: 5635
title: Licensing NFT
description: An oracle for retrieving NFT licensing agreements
author: Timi (@0xTimi), 0xTriple7 (@ysqi)
discussions-to: https://ethereum-magicians.org/t/eip-5635-discussion-nft-licensing-agreement-standard/10779
status: Draft
type: Standards Track
category: ERC
created: 2022-08-10
requires: 165, 721, 1155
---

## Abstract

This EIP standardizes an NFT licensing oracle to store (register) and retrieve (discover) granted licensing agreements for non-fungible token (NFT) derivative works, which are also NFTs but are created using properties of some other underlying NFTs.

In this standard, an NFT derivative work is referred to as a **dNFT**, while the original underlying NFT is referred to as an **oNFT**.

The NFT owner, known as the `licensor`, may authorize another creator, known as the `licensee`, to create a derivative works (dNFTs), in exchange for an agreed payment, known as a `Royalty`. A licensing agreement outlines terms and conditions related to the deal between the licensor and licensee.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.

In general, there are three important roles in this standard:

- oNFT: An original underlying NFT. The holder of an oNFT is a licensor. An oNFT can be any NFT.
- dNFT: A derivative work based on one or more oNFTs. The holder of a dNFT is a licensee.

1. Any compliant contract MUST implement the `IERC5635` interface.
1. Any compliant contract MUST implement the `IERC1155` or `IERC721` interface.
1. Any compliant contract MUST implement the [EIP-165](./eip-165.md) `supportsInterface` function and MUST return the constant value `true` if `0x8cd02adc` is passed through the `interfaceID` argument.

```solidity

/**
 * @title EIP-5635 Licensing Token Standard.
 * @dev See https://eips.ethereum.org/EIPS/eip-5635
 * Note: The ERC-165 identifier for this interface is 0x8cd02adc.
 */

interface IERC5635 {
    /// @dev The licensing argeement meta data.
    struct Licensing {
        uint256 oNFTId;
        address oNFT;
        // License expiry timestamp.
        uint64 expiryTime;
        // The royalty rate as an unsigned integer, scaled by 1e8.
        uint32 royaltyRateMantissa;
        // The indentifier, e.g. `MIT` or `Apache`
        // see: https://spdx.org/licenses/
        string licenseId;
        // The NFT Licensing Argeement Type.
        string argeementId;
        // Additional licensing information for extended licensing.
        bytes extraData;
    }

    /**
     * @dev Safely transfers one `tokenId` token from `from` to `to`.
     * see https://eips.ethereum.org/EIPS/eip-721
     * https://eips.ethereum.org/EIPS/eip-1155
     */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;

    /**
     * @dev Retruns the licensing argeement meta data.
     *
     * MUST revert if `tokenId` not found.
     * @param tokenId ID of the licensing token.
     */
    function licenseInfo(uint256 tokenId) external view returns (Licensing memory);

    /// @notice Called with the sale price to determine how much royalty
    //          is owed and to whom.
    /// @param  tokenId        ID of the licensing token.
    /// @param  salePrice      The sale price of the dNFT asset specified.
    /// @return receiver       Address of who should be sent the royalty payment
    /// @return royaltyAmount  The royalty payment amount for salePrice
    function royaltyInfo(uint256 tokenId, uint256 salePrice)
        external
        view
        returns (address receiver, uint256 royaltyAmount);
}
```

In order to allow a thrid party to query the authorization license:

1. the `dNFT` contract
must implement the `IERC5635Derivative` and `IERC165` inferfaces.

1. Any compliant dNFT contract MUST implement the `IERC5635Derivative` interface.
1. Any compliant dNFT contract MUST implement the EIP-165 `supportsInterface` function and MUST return the
   constant value `true` if `0xf8c79e7e` is passed through the `interfaceID` argument.

```solidity
/**
 * @title EIP-5635 Derivatives NFT (dNFT) Standard.
 * @dev See https://eips.ethereum.org/EIPS/eip-5635
 * Note: The ERC-165 identifier for this interface is 0xf8c79e7e.
 */
interface IERC5635Derivative {
    struct LicenseNFT {
        address token;
        uint256 tokenId;
    }

    /**
     * @dev MUST emit when token is bound to a licensing token.
     * @param tokenId           ID of the derivative token.
     * @param bindIndex         Index on the licensing list of this token.
     * @param licensingToken    Address of licensing token contract.
     * @param licensingTokenId  ID of the licensing token.
     */
    event Bound(uint256 indexed tokenId, uint256 bindIndex, address licensingToken, uint256 licensingTokenId);

    /**
     * @notice Bind a licensing token to `tokenId` token.
     * @dev Caller must be token holder.
     *
     * When binding, the licensing will be transferred to this contract. and locked,
     * and is is not allowed to be used again.
     * The licensing holder must approve this contract to transfer it before call
     * transfer method.
     *
     * MUST revert if tokenId not found.
     * MUST revert if caller is not token minner.
     * MUST revert if licenseToken doese not supprot IERC5635 interface.
     * MUST revert if licenseToken doese not supprot IERC721 nor IERC1155 interface.
     * MUST revert if transfer licensing failed.
     * MUST revert on any other error.
     * MUST emit Bound event.
     *
     * @param tokenId           ID of the derivative token.
     * @param licensingToken    Address of licensing token contract.
     * @param licensingTokenId  ID of the licensing token.
     */
    function bindLicense(uint256 tokenId, address licensingToken, uint256 licensingTokenId) external;

    /**
     * @dev Returns the all licensing NFTs of `tokenId`.
     *
     * MUST revert if tokenId not found.
     *
     * @param tokenId          ID of the derivative token.
     * @return LicenseNFT[]    A list of licensing NFT, including token address and token id.
     * Returns empty list if the given token is not bound to a licensing NFT.
     */
    function licensingNFTs(uint256 tokenId) external view returns (LicenseNFT[] memory);
}
```

## Rationale

Licensing credentials from a dNFT's contract can be retrieved with `licenseingInfo` method,
which specifies the details of a licensing agreement, which may include the oNFT.

When a dNFT is minted, miners associate a license for the dNFT via the `bindLicense` method.

Therefore, miners need to obtain licensing Token from the marketplace or dNFT owner before binding.

Anyone can retrieve licensing royalty information with `royaltyInfo` via the licensing token.
While it is not possible to enforce the rules set out in this EIP on-chain, just like
[EIP-2981](./eip-2981.md), we encourages NFT marketplaces to follow this EIP.

### Two stages: Licensing and Discovery

Taking the moment when the dNFT is minted as the cut-off point, the stage before is called the
**Licensing** stage, and the subsequent stage is called the **Discovery** stage. The interface
`IERC5635` is for the **Licensing** stage, and the interfaces `IERC5635Derivative` are for the
**Discovery** stage.

### Design decision: beneficiary of licensing agreement

As soon as someone sells their NFT, the full licensed rights are passed along to the new owner without any encumbrances, so that the beneficiary should be the new owner.

## Backwards Compatibility

This standard is compatible with [EIP-721](./eip-721.md), [EIP-1155](./eip-1155.md), and [EIP-2981](./eip-2981.md).

## Reference Implementation

1. [5635.sol](../assets/eip-5635/ERC5635.sol);
1. [5635Derivative.sol](../assets/eip-5635/ERC5635Derivative.sol)

### Examples

```solidity
bytes4 private constant _INTERFACE_ID_ERC5635 = 0x8cd02adc;
bytes4 private constant _INTERFACE_ID_ERC5635Derivative = 0xf8c79e7e;
```

#### Deploying an [EIP-721](./eip-721.md) NFT and signaling support for dNFT

```solidity
constructor (string memory name, string memory symbol, string memory baseURI) {
        _name = name;
        _symbol = symbol;
        _setBaseURI(baseURI);
        // register the supported interfaces to conform to ERC721 via ERC165
        _registerInterface(_INTERFACE_ID_ERC721);
        _registerInterface(_INTERFACE_ID_ERC721_METADATA);
        _registerInterface(_INTERFACE_ID_ERC721_ENUMERABLE);

        // dNFT interface
        _registerInterface(_INTERFACE_ID_ERC5635Derivative);
}
```

#### Checking if the NFT being sold on your marketplace is a dNFT

```solidity
function isERC5635DNFT(address _contract) internal returns (bool) {
    return IERC165(_contract).supportsInterface(_INTERFACE_ID_ERC5635Derivative);
}
```

#### Checking if an token is EIP-5635

```solidity
function isERC5635(address _contract) internal returns (bool) {
    return IERC165(_contract).supportsInterface(_INTERFACE_ID_ERC5635);
}
```

## Security Considerations

Needs discussion.


## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
